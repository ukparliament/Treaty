PREFIX : <https://id.parliament.uk/schema/>
PREFIX id: <https://id.parliament.uk/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

CONSTRUCT {
    ?workPackagedThing
        a ?workPackagedThingType ;
        :workPackagedThingHasWorkPackage ?workPackage ;
        :workPackagedThingHasWorkPackagedThingWebLink ?workPackagedThingWebLink ;
        :laidThingHasLaying ?laying ;
        :treatyCommandPaperNumber ?treatyCommandPaperNumber ;
        :treatyCommandPaperPrefix ?treatyCommandPaperPrefix ;
        :treatyName ?treatyName ;
        :inForceTreatyHasTreatySeriesMembership ?inForceTreatySeriesMembership ;
        :treatyHasSeriesMembership ?treatySeriesMembership ;
        ?seriesMembership ?treatySeriesMembership ;
        :treatyHasLeadGovernmentOrganisation ?treatyLeadGovernmentOrganisation .

    ?workPackage
        a :WorkPackage ;
        :workPackageHasBusinessItem ?businessItem ;
        :workPackageHasProcedure ?procedure .

    ?businessItem
        a :BusinessItem ;
        :businessItemDate ?businessItemDate ;
        :businessItemHasBusinessItemWebLink ?businessItemWebLink ;
        :businessItemHasProcedureStep ?procedureStep .

    ?procedureStep
        a :ProcedureStep ;
        :procedureStepName ?procedureStepName ;
        :procedureStepHasHouse ?house .

    ?procedure
        a :Procedure ;
        :procedureName ?procedureName .

    ?laying
        a ?layingType ;
        :layingDate ?layingDate ;
        :layingHasLayingBody ?layingBody .

    ?layingBody
        a ?layingBodyType ;
        :groupName ?layingBodyGroupName .

    ?treatyLeadGovernmentOrganisation
        a :GovernmentOrganisation ;
        :groupName ?treatyLeadGovernmentOrganisationGroupName .

    ?treatySeriesMembership
        a ?treatySeriesMembershipType ;
        :seriesItemCitation ?treatySeriesItemCitation .

    ?house
        a :House ;
        :houseName ?houseName .
}
WHERE {
    BIND(@id as ?workPackagedThing)

    {
        # Work packaged thing has workpackage and workpackage has procedure
        ?workPackagedThing :workPackagedThingHasWorkPackage ?workPackage .

        ?workPackage :workPackageHasProcedure ?procedure .

        ?procedure :procedureName ?procedureName .

        OPTIONAL { ?workPackagedThing :workPackagedThingHasWorkPackagedThingWebLink ?workPackagedThingWebLink . }

        # Business items and actualised procedure steps details for work package
        OPTIONAL
        {
            ?workPackage :workPackageHasBusinessItem ?businessItem .

            ?businessItem :businessItemHasProcedureStep ?procedureStep .

            ?procedureStep :procedureStepName ?procedureStepName .

            OPTIONAL { ?businessItem :businessItemDate ?businessItemDate . }

            OPTIONAL { ?businessItem :businessItemHasBusinessItemWebLink ?businessItemWebLink . }

            OPTIONAL
            {
                ?procedureStep :procedureStepHasHouse ?house .
                ?house :houseName ?houseName .
            }
        }

        # Treaty specific predicates
        OPTIONAL { ?workPackagedThing :treatyName ?treatyName . }
        
        OPTIONAL
        {
            ?workPackagedThing :treatyHasLeadGovernmentOrganisation ?treatyLeadGovernmentOrganisation .

            ?treatyLeadGovernmentOrganisation :groupName ?treatyLeadGovernmentOrganisationGroupName .
        }

        OPTIONAL
        {
            ?workPackagedThing
                :treatyCommandPaperPrefix ?treatyCommandPaperPrefix ;
                :treatyCommandPaperNumber ?treatyCommandPaperNumber .
        }

        OPTIONAL { ?workPackagedThing :inForceTreatyHasTreatySeriesMembership ?inForceTreatySeriesMembership . }

        OPTIONAL
        {
            ?workPackagedThing
                :treatyHasSeriesMembership ?treatySeriesMembership ;
                ?seriesMembership ?treatySeriesMembership .

            ?treatySeriesMembership
                a ?treatySeriesMembershipType ;
                :seriesItemCitation ?treatySeriesItemCitation .
        }
    }
    UNION
    {
        # Laid thing details
        ?workPackagedThing :laidThingHasLaying ?laying .

        ?laying
            a ?layingType ;
            :layingDate ?layingDate ;
            :layingHasLayingBody ?layingBody .

        ?layingBody
            a ?layingBodyType ;
            :groupName ?layingBodyGroupName .
    }
    UNION
    {
        # Work packaged thing types
        ?workPackagedThing a ?workPackagedThingType .
    }
}
